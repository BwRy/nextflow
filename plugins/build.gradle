import io.nextflow.gradle.PluginsRepositoryIndex
import io.nextflow.gradle.S3Upload
import org.apache.commons.codec.digest.DigestUtils

apply plugin: 'java'
apply plugin: "nextflow-plugin"

jar.enabled = false

String computeSha512(File file) {
    if( !file.exists() )
        throw new GradleException("Missing file: $file -- cannot compute SHA-512")
    return DigestUtils.sha512Hex(file.bytes)
}

String now() {
    "${java.time.OffsetDateTime.now().format(java.time.format.DateTimeFormatter.ISO_DATE_TIME)}"
}

def timestamp = now()

ext.s3PublishUrl = 's3://www.nextflow.io/plugins'
ext.httpPublishUrl = 'http://www.nextflow.io.s3-website-eu-west-1.amazonaws.com/plugins'

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: "nextflow-plugin"
    
    repositories {
        mavenLocal()
        mavenCentral()
    }

    /*
     * Creates plugin zip and json meta file in plugin `build/libs` directory
     */
    task plugin(type: Jar) {
        into('classes') {
            with jar
        }
        into('lib') {
            from configurations.compile
        }
        archiveExtension ='zip'

        doLast {
            // create the meta file
            final zip = new File("$buildDir/libs/${project.name}-${project.version}.zip")
            final meta = new File("$buildDir/libs/${project.name}-${project.version}.json")
            meta.text = """
              {
                "version": "${project.version}",
                "date": "${timestamp}",
                "url": "$httpPublishUrl/${project.name}/${project.name}-${project.version}.zip",
                "sha512sum": "${computeSha512(zip)}"
              }
            """.stripIndent()
        }
    }

    task assemblePlugin(type: Copy) {
        from plugin
        into pluginsDir
    }

    task targetLibs(type: Sync) {
        from configurations.compile
        into 'build/target/libs'
    }

    task uploadPluginZip(type: S3Upload, dependsOn: plugin) {
        // the provider mess is needed to lazy evaluate the `project.version` property
        //   https://docs.gradle.org/current/userguide/lazy_configuration.html#lazy_properties
        //   https://stackoverflow.com/questions/13198358/how-to-get-project-version-in-custom-gradle-plugin/13198744
        source = providers.provider { "$buildDir/libs/${project.name}-${project.version}.zip" }
        target = providers.provider { "$s3PublishUrl/${project.name}/${project.name}-${project.version}.zip" }
        overwrite = true
        publicRead = true
    }

    task uploadPluginMeta(type: S3Upload, dependsOn: plugin) {
        source = providers.provider { "$buildDir/libs/${project.name}-${project.version}.json" }
        target = providers.provider { "$s3PublishUrl/${project.name}/${project.name}-${project.version}.json" }
        overwrite = true
        publicRead = true
    }

    task uploadPlugin(dependsOn: [uploadPluginMeta, uploadPluginZip] ) {
    }
}

task uploadRepositoryIndex(type: PluginsRepositoryIndex) {
    publishUrl = s3PublishUrl
}

task uploadPlugins(dependsOn: [subprojects.uploadPlugin]) {
}

task assemblePlugins(type: Copy, dependsOn: [subprojects.assemblePlugin, subprojects.targetLibs]) {
}

assemble.dependsOn subprojects.assemblePlugin, subprojects.targetLibs
